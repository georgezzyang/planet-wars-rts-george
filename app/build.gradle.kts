import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    kotlin("jvm") version "2.1.0"
    kotlin("plugin.serialization") version "2.1.0"
    application
    id("com.github.johnrengelman.shadow") version "8.1.1"
}



repositories {
    mavenCentral()
}

group = "sml"
version = "1.0-SNAPSHOT"

dependencies {
    // Kotlin Standard Library
    implementation("org.jetbrains.kotlin:kotlin-stdlib:2.1.0")

    // Ktor dependencies
    implementation("io.ktor:ktor-server-core:2.3.3")
    implementation("io.ktor:ktor-server-netty:2.3.3")
    implementation("io.ktor:ktor-server-websockets:2.3.3")
    implementation("io.ktor:ktor-client-core:2.3.3")
    implementation("io.ktor:ktor-client-cio:2.3.3")
    implementation("io.ktor:ktor-server-content-negotiation:2.3.3")
    implementation("io.ktor:ktor-serialization-kotlinx-json:2.3.3")

    // Kotlinx Serialization
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1")

    // Coroutines
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.7.3")

    // Testing
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.10.0")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    // Additional Libraries
    implementation("com.google.guava:guava:32.1.2-jre")
}
tasks.withType<Test>().configureEach {
    useJUnitPlatform()
}

tasks.withType<Jar> {
    manifest {
        attributes["Main-Class"] = "competition_entry.RunEntryAsServerKt"
    }
}

tasks.named<ShadowJar>("shadowJar") {
    archiveBaseName.set("client-server")
    archiveClassifier.set("")
    archiveVersion.set("")
    manifest {
        attributes["Main-Class"] = "competition_entry.RunEntryAsServerKt"
    }
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21) // Java 21 LTS - best compatibility
    }
}

application {
    mainClass.set("competition_entry.RunEntryAsServerKt") // Adjust this if your package structure is different
}

kotlin {
    jvmToolchain(21) // Ensure Kotlin targets JVM 21 as well
}

tasks.register<JavaExec>("runEvaluation") {
    mainClass.set("games.planetwars.runners.EvaluateAgentKt")
    classpath = sourceSets["main"].runtimeClasspath
    args = listOf(project.findProperty("args")?.toString() ?: "49875")
}

tasks.register<JavaExec>("runUnifiedExample") {
    mainClass.set("games.planetwars.runners.UnifiedGameRunnerKt")
    classpath = sourceSets["main"].runtimeClasspath
    standardInput = System.`in`
}
tasks.register<JavaExec>("benchmarkNaiveMCTS") {
    mainClass.set("competition_entry.LocalBenchmarkKt")
    classpath = sourceSets["main"].runtimeClasspath
}

tasks.register<JavaExec>("benchmarkMaps") {
    mainClass.set("competition_entry.MapBenchmarkKt")
    classpath = sourceSets["main"].runtimeClasspath
}

tasks.register<JavaExec>("runRemotePairEvaluation") {
    // Kotlin entry point above
    mainClass.set("games.planetwars.runners.RunRemotePairEvaluationKt")
    classpath = sourceSets["main"].runtimeClasspath

    // Support `--args=portA,portB,gpp,timeout` (comes in as a project property)
    val raw = project.findProperty("args")?.toString()
    args = if (raw != null) listOf(raw) else listOf("5001,5002,10,50")
}
